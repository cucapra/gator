tag model is vec3;
tag world is vec3;
tag camera is vec3;
tag hom<`t : vec3> is vec4;
tag norm<`t : vec> is `t;
tag color is vec3;
tag alpha<`t : color> is vec4;

declare canon norm<`t> normalize<`t : vec>(`t x);
declare float dot<`t: vec>(norm<`t> v1, norm<`t> v2);
declare vec4 vec4(vec3 v, float f);
declare vec3 vec3(vec4 v);
declare float min(float f1, float f2);
declare float max(float f1, float f2);
declare `t reflect<`t : vec>(`t v1, `t v2);
declare float pow(float f1, float f2);

varying model vPosition;
canon uniform hom<model>->hom<world> uModel;
canon uniform hom<world>->hom<camera> uView;
varying norm<model> vNormal;
uniform world uLight;

canon hom<`t> homify<`t : vec3>(`t v) { return vec4(v as vec3, 1.) as hom<`t>; }
canon `t hom_reduce<`t : vec3>(hom<`t> v) { return vec3(v as vec4) as `t; }
canon hom<`t> homify_normal<`t : vec3>(norm<`t> v) { return vec4(v as vec3, 0.) as hom<`t>; }
canon alpha<color> extendColor(color v) { return vec4(v as vec3, 1.) as alpha<color>; }

void main() {
    color ambient = [.1, 0., 0.];
    color diffuse = [0.4, 0.3, 0.9];
    color spec = [1.0, 1.0, 1.0];

    auto lightDir = normalize(uLight - (vPosition in world));
    float lambertian = max(dot(vNormal in norm<world>, lightDir), 0.);
    
    auto reflectDir = normalize(reflect(-lightDir, vNormal in norm<world>) in camera);
    float specular = pow(max(dot(-(vPosition in norm<camera>), reflectDir), 0.0), 32.);

    alpha<color> gl_FragColor = extendColor(ambient + lambertian * diffuse + specular * spec);
}
