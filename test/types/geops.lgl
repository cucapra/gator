type vec2 is float[2];
type vec3 is float[3];
type vec4 is float[4];
type scalar is float;

declare T +<T:vec2>(T v1, T v2);
declare T -<T:vec2>(T v1, T v2);
declare T -<T:vec2>(T v);
declare T *<T:vec2,U:float>(T v, U f);
declare T *<T:vec2,U:float>(U f, T v);

declare T +<T:vec3>(T v1, T v2);
declare T -<T:vec3>(T v1, T v2);
declare T -<T:vec3>(T v);
declare T *<T:vec3,U:float>(T v, U f);
declare T *<T:vec3,U:float>(U f, T v);

declare T +<T:vec4>(T v1, T v2);
declare T -<T:vec4>(T v1, T v2);
declare T -<T:vec4>(T v);
declare T *<T:vec4,U:float>(T v, U f);
declare T *<T:vec4,U:float>(U f, T v);

declare T +<T:float>(T f1, T f2);
declare T -<T:float>(T f1, T f2);
declare T -<T:float>(T f);
declare T *<T:float>(T f1, T f2);
declare T /<T:float>(T f1, T f2);

declare float cos(float f);
declare float sqrt(float f);
declare float acos(float f);

prototype geometry {
    object point<r>;
    object vector<r>;

    vector<r> +(vector<r> x, vector<r> y);
    vector<r> -(vector<r> x, vector<r> y);
    vector<r> *(vector<r> v, scalar s);
    vector<r> *(scalar s, vector<r> v);
    vector<r> -(vector<r> v);
    point<r> translate(point<r> p, vector<r> v);
    point<r> +(point<r> p, vector<r> v);
    point<r> -(point<r> p, vector<r> v);
    vector<r> -(point<r> x, point<r> y);
    vector<r> -(point<r> x);
}
coordinate cart2 : geometry {
    dimension 2; // i.e. all type parameterizations are implicitly frames of dimension 2

    object point<r> is float[2];
    object vector<r> is float[2];

    vector<r> +(vector<r> x, vector<r> y) {
        return (x as! vec2 + y as! vec2) as! vector<r>;
    }
    vector<r> -(vector<r> x, vector<r> y) {
        return (x as! vec2 - y as! vec2) as! vector<r>;
    }
    vector<r> *(vector<r> v, scalar s) {
        return (v as! vec2 * s) as! vector<r>;
    }
    vector<r> *(scalar s, vector<r> v) {
        return (s * v as! vec2) as! vector<r>;
    }
    vector<r> -(vector<r> v) {
        return (-v as! vec2) as! vector<r>;
    }
    point<r> translate(point<r> p, vector<r> v) {
        return (p as! vec2 + v as! vec2) as! point<r>;
    }
    point<r> +(point<r> p, vector<r> v) {
        return (p as! vec2 + v as! vec2) as! point<r>;
    }
    point<r> -(point<r> p, vector<r> v) {
        return (p as! vec2 - v as! vec2) as! point<r>;
    }
    vector<r> -(point<r> x, point<r> y) {
        return (x as! vec2 - y as! vec2) as! vector<r>;
    }
    vector<r> -(point<r> v) {
        return (-v as! vec2) as! vector<r>;
    }
}
coordinate cart3 : geometry {
    dimension 3;

    object point<r> is float[3];
    object vector<r> is float[3];

    vector<r> +(vector<r> x, vector<r> y) {
        return (x as! vec3 + y as! vec3) as! vector<r>;
    }
    vector<r> -(vector<r> x, vector<r> y) {
        return (x as! vec3 - y as! vec3) as! vector<r>;
    }
    vector<r> *(vector<r> v, scalar s) {
        return (v as! vec3 * s) as! vector<r>;
    }
    vector<r> *(scalar s, vector<r> v) {
        return (s * v as! vec3) as! vector<r>;
    }
    vector<r> -(vector<r> v) {
        return (-v as! vec3) as! vector<r>;
    }
    point<r> translate(point<r> p, vector<r> v) {
        return (p as! vec3 + v as! vec3) as! point<r>;
    }
    point<r> +(point<r> p, vector<r> v) {
        return (p as! vec3 + v as! vec3) as! point<r>;
    }
    point<r> -(point<r> p, vector<r> v) {
        return (p as! vec3 - v as! vec3) as! point<r>;
    }
    vector<r> -(point<r> x, point<r> y) {
        return (x as! vec3 - y as! vec3) as! vector<r>;
    }
    vector<r> -(point<r> v) {
        return (-v as! vec3) as! vector<r>;
    }
}
coordinate polar : geometry {
    dimension 2;

    object point<r> is float[2];
    object vector<r> is float[2];

    T __trans<T : float[2]>(T x, T y) {
        // https://pritschet.me/wiki/physics/linear-translation-polar-coordinates/
        float r1 = x[0];
        float r2 = y[0];
        float theta1 = x[1];
        float theta2 = x[2];
        float r = sqrt(r1 * r1 + r2 * r2 + 2. * r1 * r2 * cos(theta1 - theta2));
        float theta = acos((r1 * cos(theta1) + r2 * cos(theta2)) / r);
        return [r, theta];
    }
    vector<r> +(vector<r> x, vector<r> y) {
        return __trans(x, y);
    }
    vector<r> -(vector<r> x, vector<r> y) {
        return __trans(x, [-y[0], y[1]]);
    }
    vector<r> *(vector<r> v, scalar s) {
        return [v[0] * s, v[1]];
    }
    vector<r> *(scalar s, vector<r> v) {
        return [s * v[0], v[1]];
    }
    vector<r> -(vector<r> v) {
        return [-v[0], v[1]];
    }
    point<r> translate(point<r> p, vector<r> v) {
        return __trans(p, v) as! point<r>;
    }
    point<r> +(point<r> p, vector<r> v) {
        return __trans(p, v) as! point<r>;
    }
    point<r> +(point<r> p, vector<r> v) {
        return (p as! vec2 - v as! vec2) as! point<r>;
    }
    vector<r> -(point<r> x, point<r> y) {
        return __trans(x, [-y[0], y[1]]) as! vector<r>;
    }
    vector<r> -(point<r> v) {
        return [-v[0], v[1]];
    }
}
coordinate hom : geometry {
    dimension 3;

    object point<r> is float[4];
    object vector<r> is float[4];

    vector<r> +(vector<r> x, vector<r> y) {
        return [x[0] + y[0], x[1] + y[1], x[2] + y[2], 0.];
    }
    vector<r> -(vector<r> x, vector<r> y) {
        return [x[0] - y[0], x[1] - y[1], x[2] - y[2], 0.];
    }
    vector<r> *(vector<r> v, scalar s) {
        return [v[0] * s, v[1] * s, v[2] * s, 0.];
    }
    vector<r> *(scalar s, vector<r> v) {
        return [s * v[0], s * v[1], s * v[2], 0.];
    }
    vector<r> -(vector<r> v) {
        return [-v[0], -v[1], -v[2], 0.];
    }
    point<r> translate(point<r> p, vector<r> v) {
        return (p as! vec4 + v as! vec4 * p[3]) as! point<r>;
    }
    point<r> +(point<r> p, vector<r> v) {
        return (p as! vec4 + v as! vec4 * p[3]) as! point<r>;
    }
    point<r> -(point<r> p, vector<r> v) {
        return (p as! vec4 - v as! vec4 * p[3]) as! point<r>;
    }
    vector<r> -(point<r> x, point<r> y) {
        return (x as! vec4 * y[3] + y as! vec4 * x[3]) as! vector<r>;
    }
    // We don't actually need to define every function, it will just fail if we try and call an undefined function
}

frame world has dimension 3;
frame plane has dimension 2;

void main() {
    cart2.point<plane> ppos = [1., 1.];
    cart2.vector<plane> poffset = [1., 2.];

    print ppos + poffset;
    print 2. * (ppos - (ppos + poffset));
    
    cart3.vector<world> mpos = [1., 2., 3.];
    
    print mpos - mpos * 2.;

    polar.vector<plane> ppo = [1., 3.14159265];
    polar.vector<plane> ppo2 = [1., 0.];
    
    print 3. * ppo * 2.;
    print ppo + ppo2;

    hom.point<world> hpos = [1., 2., 3., 2.];
    hom.vector<world> hvec = [1., 1., 1., 0.];

    print hpos + hvec * 2.;
    print hpos - (hpos + hvec);
}
